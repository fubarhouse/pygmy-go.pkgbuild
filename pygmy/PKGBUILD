# Maintainer: Karl Hepworth <karl.hepworth@gmail.com>
pkgname=pygmy-go
pkgver=0.1.0
pkgrel=1
pkgdesc="Amazee.io's local docker development tool"
arch=(any)
url="https://github.com/fubarhouse/pygmy-go"
license=('MIT')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fubarhouse/pygmy-go/archive/v${pkgver}.tar.gz")
sha512sums=('9010fd8b8e97dd57759bcac8c807b20e542d5b049b24ea0088bab69690d526e15e22a64c6980e61548891bd4552c90a20b987eaea2825aac58af4adc38802489')

build(){
  cd "$pkgname-$pkgver"
  go mod vendor
  go build \
    -mod=vendor \
    -trimpath \
    -ldflags "-extldflags ${LDFLAGS}" \
    .
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
